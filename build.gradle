plugins {
    id 'base'
    id 'org.jetbrains.kotlin.jvm' version '1.4.0' apply false
}

allprojects {
    group 'org.netomi.sudoku'
    version '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
        jcenter()
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "kotlin"

    ext {
        junitVersion = "5.3.2"
    }

    sourceCompatibility = '11'
    targetCompatibility = '11'

    compileKotlin {
        kotlinOptions.jvmTarget = "11"
        kotlinOptions.freeCompilerArgs = [
            "-Xjvm-default=enable"
        ]
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "11"
        kotlinOptions.freeCompilerArgs = [
            "-Xjvm-default=enable"
        ]
    }

    def srcModule = "src/main/module"
    def moduleInfo = file("${project.projectDir}/$srcModule/module-info.java")
    if (moduleInfo.exists()) {
        sourceSets {
            module {
                java {
                    srcDirs = [srcModule]
                    compileClasspath = main.compileClasspath
                    sourceCompatibility = '11'
                    targetCompatibility = '11'
                }
            }
            main {
                kotlin { srcDirs += [srcModule] }
            }
        }

        compileModuleJava.configure {
            dependsOn compileKotlin
            destinationDir = compileKotlin.destinationDir
            doFirst {
                options.compilerArgs = ['--module-path', classpath.asPath,]
                classpath = files()
            }
        }
        jar.dependsOn compileModuleJava

        compileKotlin.finalizedBy compileModuleJava
    }

    jar {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}
